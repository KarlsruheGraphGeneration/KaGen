include(FindPkgConfig)
pkg_check_modules(ImageMagick IMPORTED_TARGET ImageMagick Magick++ MagickCore)

if (ImageMagick_FOUND AND ImageMagick_ImageMagick_VERSION VERSION_GREATER_EQUAL "7.0.0")
    add_executable(img2kargb img2kargb.cpp)
    target_link_libraries(img2kargb PRIVATE PkgConfig::ImageMagick)
    install(TARGETS img2kargb DESTINATION bin)

    add_executable(part2img part2img.cpp)
    target_link_libraries(part2img PRIVATE PkgConfig::ImageMagick)
    install(TARGETS part2img DESTINATION bin)
else ()
    if (NOT ImageMagick_FOUND) 
        set(REASON "could not find ImageMagick on your system")
    else ()
        set(REASON "ImageMagick found on your system is too old (version ${ImageMagick_ImageMagick_VERSION}, require >= 7.0.0)")
    endif ()
    message(STATUS "img2kargb not available: ${REASON}")
    message(STATUS "part2img not available: ${REASON}")
endif ()

add_executable(upsb2kargb upsb2kargb.cpp)
install(TARGETS upsb2kargb DESTINATION bin)

add_executable(chkmetis chkmetis.cpp)
target_link_libraries(chkmetis PRIVATE kagen)
target_include_directories(chkmetis PRIVATE ${PROJECT_SOURCE_DIR})
install(TARGETS chkmetis DESTINATION bin)
